TransitionSystem

ADT The_terrifying_journey_Of_a_little_girl

Signature

Sorts
	house, entity, journey

Generators
	// Various entity that can be in the corridor
	empty_space: entity
	afraid_girl: entity
	reassured_girl: entity
	ghost: entity

	// Build the house using a more or less long corridor
	// with a light switch at the end
	dark_corridor: entity, house -> house
	lighted_corridor: entity, house -> house
	light_switch : house

	// Little girl bedroom door
	door_open: entity, house -> journey
	door_closed: house -> journey

Variables
	house       : house
	afraid_girl : entity
	entity      : entity

// INITIAL STATE
initialState = door_open(afraid_girl, dark_corridor(
    empty_space, dark_corridor(
        empty_space, dark_corridor(
            empty_space, dark_corridor(
                empty_space, dark_corridor( 
                    empty_space, light_switch))))))

Strategies
	Try(V) = Choice(V, Identity)

	// Going through the open door of the little girl bedroom
	OneStepInTheHouse = {door_open(afraid_girl, dark_corridor(empty_space, $house)) -> 
	door_closed(dark_corridor(afraid_girl, $house))}
	Entering_the_house = Try(OneStepInTheHouse())

	// There is a ghost, the little girl is to afraid to do anything until it goes away
	Freeze = {dark_corridor(afraid_girl, dark_corridor(ghost, $house)) -> dark_corridor(afraid_girl, dark_corridor(empty_space, $house))}

	// There isn't any ghost, the little girl takes a step further into the corridor
	Take_a_step = {dark_corridor(afraid_girl, dark_corridor(empty_space, $house)) -> dark_corridor(empty_space, dark_corridor(afraid_girl, $house))}

	// The goal is to reach the light switch to be reassure that ghosts don't exist
	// EXPLAIN ForEachRoom
	ForEachRoom(V) = Choice(One(ForEachRoom(V), 2), V)
	Try_to_reach_light_switch_with_fear    = ForEachRoom(Freeze())
	Try_to_reach_light_switch_with_courage = ForEachRoom(Take_a_step())

	// If the little girl can reach the light switch, she will turn on the light
	Switch_the_light = {dark_corridor(afraid_girl, light_switch) -> dark_corridor(reassured_girl, light_switch)}

Transitions
	begin_the_terrifying_journey = Entering_the_house()

	// EXPLAIN THE ONE in continue_the_terrifying_journey_with_fear
	continue_the_terrifying_journey_with_fear    = One(Try_to_reach_light_switch_with_fear(), 1)
	continue_the_terrifying_journey_with_courage = One(Try_to_reach_light_switch_with_courage(), 1)

	end_the_terrifying_journey = One(ForEachRoom(Switch_the_light()), 1)

